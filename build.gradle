import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

group = "meteordevelopment"
version = "0.1.3"

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.5"
    compileOnly "com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.0.1"
    compileOnly "net.luckperms:api:5.3"

    implementation 'org.reflections:reflections:0.10.2'
    implementation "org.openlabtesting.netty:netty-all:4.1.48.Final"
    implementation "com.github.Querz:NBT:5.5"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.WARN

    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

task copyJar(type: Copy) {
    from "build/libs/meteor-pvp-" + version + ".jar"
    into "run/plugins"
}

shadowJar {
    classifier = ""
}

build.dependsOn shadowJar
